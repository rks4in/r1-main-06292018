apply plugin: 'com.android.application'
apply from: "${rootDir}/gradle/androidProject.gradle"
apply from: 'downloadSdk.gradle'

android {
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libFramework.ProtobufComms.MemChannel.so'
        pickFirst 'lib/x86_64/libFramework.ProtobufComms.MemChannel.so'
    }
}

dependencies {
    /* Base */
    implementation "com.tomtom.navui:Modules_NavAppBaseModule:${versions.navui}"
    /* MapManagement */
    implementation "com.tomtom.navui:Modules_MapManagementModule:${versions.navui}"
    implementation "com.tomtom.navui:Kits_MapCoreKit:${versions.navui}"
    /* FocusUi */
    implementation "com.tomtom.navui:Modules_FocusUiModule:${versions.navui}"
    /* Audio */
    implementation "com.tomtom.navui:Modules_PromptModule:${versions.navui}"
    /* SystemPort */
    implementation "com.tomtom.navui:Ports_StockSystemPort:${versions.navui}"
    /* ApiKit */
    implementation "com.tomtom.navui:Kits_SignatureApiKit:${versions.navui}"
    implementation "com.tomtom.navui:Kits_UtilKit:${versions.navui}"
    /* NavKit */
    implementation "com.tomtom.navui.navkit:NavKitServiceAar:${versions.navkit}"
    implementation "com.tomtom.navui.navkit:PositioningServiceAar:${versions.navkit}"
    /* Services connector */
    implementation "com.tomtom.navui.navkit:ServicesConnectorAar:${versions.navkit}"
    /* Local projects*/
    localDependency(project, ':Kits::R1CoreKit')
    localDependency(project, ':Kits::R1AppKit')
    localDependency(project, ':Libs::LocationActionHandler')
    localDependency(project, ':Ports:R1SystemPort')
}

configurations.all { Configuration configuration ->
    //allow all configurations to be visible and resolvable for dependency lock plugin
    configuration.setCanBeConsumed(true)
    configuration.setCanBeResolved(true)

    resolutionStrategy {
        componentSelection.all { ComponentSelection selection ->
            if (selection.candidate.group == "com.tomtom.navui.navkit" &&
                !selection.candidate.version.endsWith("-${versions.navkitRelease}"))
            {
                selection.reject("version of navkit packages should end with \"-${versions.navkitRelease}\"")
            }
        }
    }
}

assemble.finalizedBy(downloadSdk)

